[@强化系统初始化]
{
#if
#act
  mov S$SynthesisBoxDialogStr
  mov S$SynthesisBoxDialogButtonStr
  mov N$IsLuck 6
  mov N$IsProtect 6
  mov N$LuckCount 0
  mov N$ProtectCount 0
  mov N$MaterialID 1130
  mov N$NextRate 100
  ReturnBoxItem 0
  OPENMERCHANTBIGDLG 9 65 0 4 0 -40 1 520 50 1
  goto @显示强化系统对话框
;  SCREENEFFECT 300 400 10 410 18 1 100 1 1 

[@显示强化系统对话框]

#if
  CheckBoxItemCount 0  
#act
  mov N$UpgradeCount <$BOXITEM[0].UPGRADECOUNT>
#elseact
  mov N$UpgradeCount 0  
  mov N$IsLuck 6
  mov N$IsProtect 6
  mov N$LuckCount 0
  mov N$ProtectCount 0
  mov N$MaterialID 1130
  mov N$NextRate 100 
  mov S$SynthesisBoxDialogStr
  mov S$SynthesisBoxDialogButtonStr

#if
  equal N$LuckCount 0
#act
  mov N$IsLuck 6
  
#if
  not CheckBoxItemCount 0  
#act
  mov N$NextRate 100
#elseact 
  mov N$NextRate <$str(S$UpgradeSearchResult[0][0])>
    
#if
  equal N$IsLuck 7
#act
  inc N$NextRate <$str(S$UpgradeSearchResult[0][1])>
#if
  large N$NextRate 100
#act
  mov N$NextRate 100
  
  
  
#if
#say
  <text:装备强化:240:40{FCOLOR=253}>\
  <text:装备强化栏:152:90{FCOLOR=251}>\
  <ITEMBOX:0:9:5:170:140:50:50:*:请放入需要强化的装备>\
  <text:强化等级：:140:153{FCOLOR=250}>
  <ImgNum:8:<$str(N$UpgradeCount)>:-3:140:143>\
  <text:成功几率：:140:162{FCOLOR=250}>
  <ImgNum:1:<$str(N$NextRate)>:-3:140:160>
  <text:%:143:162{FCOLOR=250}>\
  <Img:<$str(N$IsLuck)>:9:80:185/@使用幸运石><text:幸运石:82:189{FCOLOR=251}>
  <text:强化材料:103:172{FCOLOR=251}>
  <Img:<$str(N$IsProtect)>:9:120:185/@使用守护符><text:守护符:122:189{FCOLOR=251}>\
　<ItemShow:1128:<$str(N$LuckCount)>:78:195:-1/@使用幸运石>
　<ItemShow:<$str(N$MaterialID)>:1:96:177:-1>
　<ItemShow:1129:<$str(N$ProtectCount)>:116:195:-1/@使用守护符>\
  <text:1、请将需要强化的装备放入装备强化栏:50:229{FCOLOR=250}>\
  <text:2、如果需要使用幸运石、守护符,点击图标即可:50:232{FCOLOR=250}>\
  <text:强化说明：强化越高,几率越低,使用幸运符可以获得成功率的提升:100:272{FCOLOR=70}>\
  <text:注意：强化不会导致装备被破坏,但是失败后有几率会掉星:120:287{FCOLOR=253}>\
<$str(S$SynthesisBoxDialogStr)>
<$str(S$SynthesisBoxDialogButtonStr)>
  
[@ItemOutBox0]
#if
#act
  mov S$SynthesisBoxDialogStr
  mov S$SynthesisBoxDialogButtonStr
  goto @显示强化系统对话框
  
[@ItemIntoBox0]
#if
  not CheckBoxItemCount 0  
#act
  SendMsg 5 请放入需要强化的装备
  break
#elseact
  goto  @updateBoxDialog
[@updateBoxDialog]
#if
  not CheckBoxItemCount 0  
#act
  mov S$SynthesisBoxDialogStr
  mov S$SynthesisBoxDialogButtonStr
  goto @显示强化系统对话框
  break

#if
#act
  mov S$SynthesisBoxDialogStr
  mov S$SynthesisBoxDialogButtonStr
  mov N$BoxIdx <$BOXITEM[0].Idx>
  mov S$EquipName <$BOXITEM[0].NAME_G>
  mov N$StdMode <$BOXITEM[0].STDMODE>
  mov N$UpgradeCount <$BOXITEM[0].UPGRADECOUNT>
  mov N$NeedLevel <$BOXITEM[0].NeedLevel>
  mov N$MaterialID 1130
  inc N$UpgradeCount 1
  mov N$Overlap 2  
  mov N$LuckCount 0
  mov N$ProtectCount 0
  $compute iif({0}<61,1130,iif({0}<71,1131,iif({0}<81,1132,iif({0}<91,1133,iif({0}<101,1134,iif({0}<111,1135,iif({0}<121,1136,iif({0}<131,1137,iif({0}<141,1138,iif({0}<151,1139,1139)))))))))) <$str(N$NeedLevel)> N$MaterialID 
  GetDBIdxItemFieldValue N$BoxIdx Overlap N$Overlap
  
#If
  Large  N$Overlap 1 
#act
  messagebox 无法强化首饰盒与神佑装备!
  ReturnBoxItem  0
  goto @显示强化系统对话框
  break
   
  
#if
  Equal N$BoxIdx 0
#act
  messagebox 当前物品无法强化!
  ReturnBoxItem  0
  goto @显示强化系统对话框
  break
  
  
#if
  $empty <$str(S$EquipName)>
#act
  mov S$EquipName <$BOXITEM[0].NAME>

#if
  $empty <$str(S$EquipName)>
#act
  messagebox 当前物品无法强化!
  ReturnBoxItem  0
  goto @显示强化系统对话框
  break
#if
  CheckStringlength <$str(S$EquipName)> <= 5
#act
  mov S$EquipName 　<$str(S$EquipName)>
#if
  CheckStringlength <$str(S$EquipName)> <= 7
#act
  mov S$EquipName 　<$str(S$EquipName)>

#if
#act
  $s_gets_str 装备强化.查询.主体.sql,<$str(N$UpgradeCount)>,<$str(N$BoxIdx)>,<$str(N$StdMode)> S$UpgradeSearchResult
  mov N$LuckCount <$str(S$UpgradeSearchResult[0][3])>
  mov N$ProtectCount <$str(S$UpgradeSearchResult[0][4])>
  
#if
  equal S$UpgradeSearchResult.len 0
#act
  messagebox 当前物品无法强化!
  ReturnBoxItem  0
  goto @显示强化系统对话框
  break  
  
  
#or
  equal N$StdMode 5
  equal N$StdMode 19
  equal N$StdMode 20
  equal N$StdMode 21
  equal N$StdMode 23
  equal N$StdMode 24
#act
  mov S$SynthesisBoxDialogStr
  mov S$SynthesisBoxDialogButtonStr
  GetDBIdxItemFieldValue N$BoxIdx Dc2 N$MaxDC 
  GetDBIdxItemFieldValue N$BoxIdx Sc2 N$MaxSC 
  GetDBIdxItemFieldValue N$BoxIdx Mc2 N$MaxMC 
  inc S$SynthesisBoxDialogStr <text:<$str(S$EquipName)>:380:-53{FCOLOR=253}>\
  inc S$SynthesisBoxDialogStr <text:失败掉级：:340:-16{FCOLOR=250}><ImgNum:5:<$str(S$UpgradeSearchResult[0][2])>:-3:340:-20>\
  inc S$SynthesisBoxDialogStr <text:属性加成：:340:-11{FCOLOR=250}>\
  inc S$SynthesisBoxDialogStr <text:绿色代表成功后总加成属性:350:-9{FCOLOR=70}>\
  inc S$SynthesisBoxDialogStr <text:该部位无法强化物理防御:350:-7{FCOLOR=35}>\
  inc S$SynthesisBoxDialogStr <text:该部位无法强化魔法防御:350:-5{FCOLOR=35}>\
  inc S$SynthesisBoxDialogStr <text:物理攻击：<$BOXITEM[0].LDC>-<$str(N$MaxDC)>:350:-3{FCOLOR=255}><text:(+<$str(S$UpgradeSearchResult[0][7])>):352:-3{FCOLOR=250}>\
  inc S$SynthesisBoxDialogStr <text:魔法攻击：<$BOXITEM[0].LMC>-<$str(N$MaxMC)>:350:-1{FCOLOR=255}><text:(+<$str(S$UpgradeSearchResult[0][8])>):352:-1{FCOLOR=250}>\
  inc S$SynthesisBoxDialogStr <text:道术攻击：<$BOXITEM[0].LSC>-<$str(N$MaxSC)>:350:1{FCOLOR=255}><text:(+<$str(S$UpgradeSearchResult[0][9])>):352:1{FCOLOR=250}>\
  inc S$SynthesisBoxDialogButtonStr <imgex:9:32:33:34:370:25:/@开始强化(1)><text:开始强化:295:36{FCOLOR=251}>\  
  goto @显示强化系统对话框
  break
#or
  equal N$StdMode 10
  equal N$StdMode 11
  equal N$StdMode 15
  equal N$StdMode 26
  equal N$StdMode 22
  equal N$StdMode 62
  equal N$StdMode 64
#act
  mov S$SynthesisBoxDialogStr
  mov S$SynthesisBoxDialogButtonStr
  GetDBIdxItemFieldValue N$BoxIdx Ac2 N$MaxAC 
  GetDBIdxItemFieldValue N$BoxIdx MAc2 N$MaxMAC 
  GetDBIdxItemFieldValue N$BoxIdx Dc2 N$MaxDC 
  GetDBIdxItemFieldValue N$BoxIdx Sc2 N$MaxSC 
  GetDBIdxItemFieldValue N$BoxIdx Mc2 N$MaxMC 
  inc S$SynthesisBoxDialogStr <text:<$str(S$EquipName)>:380:-53{FCOLOR=253}>\
  inc S$SynthesisBoxDialogStr <text:失败掉级：:340:-16{FCOLOR=250}><ImgNum:5:<$str(S$UpgradeSearchResult[0][2])>:-3:340:-20>\
  inc S$SynthesisBoxDialogStr <text:属性加成：:340:-11{FCOLOR=250}>\
  inc S$SynthesisBoxDialogStr <text:绿色代表成功后总加成属性:350:-9{FCOLOR=70}>\
  inc S$SynthesisBoxDialogStr <text:物理防御：<$BOXITEM[0].LAC>-<$str(N$MaxAC)>:350:-7{FCOLOR=255}><text:(+<$str(S$UpgradeSearchResult[0][5])>):352:-7{FCOLOR=250}>\
  inc S$SynthesisBoxDialogStr <text:魔法防御：<$BOXITEM[0].LMAC>-<$str(N$MaxMAC)>:350:-5{FCOLOR=255}><text:(+<$str(S$UpgradeSearchResult[0][6])>):352:-5{FCOLOR=250}>\
  inc S$SynthesisBoxDialogStr <text:物理攻击：<$BOXITEM[0].LDC>-<$str(N$MaxDC)>:350:-3{FCOLOR=255}><text:(+<$str(S$UpgradeSearchResult[0][7])>):352:-3{FCOLOR=250}>\
  inc S$SynthesisBoxDialogStr <text:魔法攻击：<$BOXITEM[0].LMC>-<$str(N$MaxMC)>:350:-1{FCOLOR=255}><text:(+<$str(S$UpgradeSearchResult[0][8])>):352:-1{FCOLOR=250}>\
  inc S$SynthesisBoxDialogStr <text:道术攻击：<$BOXITEM[0].LSC>-<$str(N$MaxSC)>:350:1{FCOLOR=255}><text:(+<$str(S$UpgradeSearchResult[0][9])>):352:1{FCOLOR=250}>\
  inc S$SynthesisBoxDialogButtonStr <imgex:9:32:33:34:370:25:/@开始强化(1)><text:开始强化:295:36{FCOLOR=251}>\   
  
  goto @显示强化系统对话框
  break
 

#if
#act
  messagebox 当前类型装备无法进行强化 
  ReturnBoxItem  0
  goto @显示强化系统对话框
  

  
[@开始强化]
#if
#act
 mov S$SynthesisBoxDialogButtonStr <imgex:9:35:35:35:370:25:><text:开始强化:295:36{FCOLOR=251}>\  
 goto @显示强化系统对话框
 mov N$NextStep 1
 SHOWPROGRESSBARDLG 2 @强化进度条完成 正在强化,%d%... 1 @强化进度条中断
 DELAYGOTO 1800 @开始强化执行

[@开始强化执行]
#if
  equal N$NextStep 1
#elseact
  goto @显示强化系统对话框
  break  
#if
#act
  mov N$NextStep 0
#if
  not CheckBoxItemCount 0  
#act
  SendMsg 5 请放入需要强化的装备
  goto  @updateBoxDialog
  break

#if
#act
  mov N$BoxIdx <$BOXITEM[0].Idx>
  mov S$EquipName 
  mov N$StdMode 0
  mov N$Overlap 2
  GetDBIdxItemFieldValue N$BoxIdx Name S$EquipName
  GetDBIdxItemFieldValue N$BoxIdx StdMode N$StdMode
  GetDBIdxItemFieldValue N$BoxIdx Overlap N$Overlap

#if
  Equal N$BoxIdx 0
#act
  messagebox 当前物品无法强化!
  ReturnBoxItem  0
  goto  @updateBoxDialog
  break

  
#If
  Large  N$Overlap 1 
#act
  messagebox 无法强化首饰盒与神佑装备!
  ReturnBoxItem  0
  goto  @updateBoxDialog
  break
   
  
  
#if
#act
  mov N$UpgradeCount <$BOXITEM[0].UPGRADECOUNT>
  inc N$UpgradeCount 1
  $compute iif({0}<61,'一{1}',iif({0}<71,'二{1}',iif({0}<81,'三{1}',iif({0}<91,'四{1}',iif({0}<101,'五{1}',iif({0}<111,'六{1}',iif({0}<121,'七{1}',iif({0}<131,'八{1}',iif({0}<141,'九{1}',iif({0}<151,'十{1}','十{1}')))))))))) <$str(N$NeedLevel)>,级强化石 S$MaterialName 
  $s_gets_str 装备强化.执行.主体.sql,<$str(N$UpgradeCount)>,<$str(N$BoxIdx)>,<$str(N$StdMode)>,<$str(N$IsLuck)> S$UpgradeMarsterExcute

  
#if
   equal <$str(S$UpgradeMarsterExcute.len)> 0
#act
  messagebox 当前物品无法强化!
  ReturnBoxItem  0
  goto  @updateBoxDialog
  break
  
#if
  equal N$IsLuck 7
  not equal S$UpgradeMarsterExcute[0][2] 0
  not CheckItem 幸运石 S$UpgradeMarsterExcute[0][2]
#act
  messagebox 你选择了使用『幸运石』,但是你的包裹没有足够的数量,强化中断! 
  goto  @updateBoxDialog
  break
#if
  EQUAL N$IsProtect 7
  not equal S$UpgradeMarsterExcute[0][3] 0
  not CheckItem 守护符 S$UpgradeMarsterExcute[0][3]
#act
  messagebox 你选择了使用『守护符』,但是你的包裹没有足够的数量,强化中断! 
  goto  @updateBoxDialog
  break
#if
  not CheckItem <$str(S$MaterialName)> 1
#act
  messagebox 你的包裹没有足够数量的『<$str(S$MaterialName)>』,强化中断! 
  goto  @updateBoxDialog
  break  
#if
  equal N$IsLuck 7
  not equal S$UpgradeMarsterExcute[0][2] 0
#act
  take 幸运石 S$UpgradeMarsterExcute[0][2]
#if
  EQUAL N$IsProtect 7
  not equal S$UpgradeMarsterExcute[0][3] 0
#act
  take 幸运石 S$UpgradeMarsterExcute[0][3]
#if
#act
  take <$str(S$MaterialName)> 1
  mov N$UpgradeFailLevel 0
  mov N$UpgradeResult 0
  $randomrang <$str(S$UpgradeMarsterExcute[0][0])>,-1 S$UpgradeResult
#if  
  equal S$UpgradeResult -1
#act
  mov N$UpgradeResult 0
#elseact
  mov N$UpgradeResult 1
#if
  EQUAL N$IsProtect 7
#act
  $randomrang 0.01,85,12,5 N$UpgradeFailLevel
  mov N$UpgradeFailLevelBase <$str(S$UpgradeMarsterExcute[0][1])>
  dec N$UpgradeFailLevelBase <$str(N$UpgradeFailLevel)>   
#elseact 
  mov N$UpgradeFailLevelBase <$str(S$UpgradeMarsterExcute[0][1])>
#if
  small N$UpgradeFailLevelBase 0
#act 
  mov   N$UpgradeFailLevel 0
#elseact 
  mov   N$UpgradeFailLevel  <$str(N$UpgradeFailLevelBase)>
  
#if
  equal N$UpgradeResult 0
  not equal N$UpgradeFailLevel 0
#act
  SendCenterMsg 180 251 强化失败!你的『<$str(S$EquipName)>』强化等级降低<$str(N$UpgradeFailLevel)>级! 0 
  dec N$UpgradeCount 1
  dec N$UpgradeCount <$str(N$UpgradeFailLevel)>
#if
  equal N$UpgradeResult 0
  equal N$UpgradeFailLevel 0  
#act
  SendCenterMsg 180 251 强化失败!你的『<$str(S$EquipName)>』没有发生改变! 0 
  goto  @updateBoxDialog
  break  
  
#if
  small N$UpgradeCount 0
#act
  mov N$UpgradeCount 0  

#if
#act
  $s_gets_str 装备强化.执行.材料.sql,<$str(N$UpgradeCount)>,<$str(N$BoxIdx)>,<$str(N$StdMode)> S$UpgradeDetailExcute  
#if
   equal <$str(S$UpgradeDetailExcute.len)> 0
#act
  messagebox 当前物品无法强化!
  ReturnBoxItem  0
  goto  @updateBoxDialog
  break

#if
#act   
  SetUpgradeItem 0
#if
  equal N$StdMode 5
  ;武器强化位置 0,1,2
#act  
  CHANGEITEMADDVALUE  boxitem0 0 = S$UpgradeDetailExcute[0][2]
  CHANGEITEMADDVALUE  boxitem0 1 = S$UpgradeDetailExcute[0][3]
  CHANGEITEMADDVALUE  boxitem0 2 = S$UpgradeDetailExcute[0][4]
  mov N$IsUpgrade 1
#or
  equal N$StdMode 19
  equal N$StdMode 20
  equal N$StdMode 21
  equal N$StdMode 23
  equal N$StdMode 24
  ;特殊位置强化位置 2,3,4
#act  
  CHANGEITEMADDVALUE  boxitem0 2 = S$UpgradeDetailExcute[0][2]
  CHANGEITEMADDVALUE  boxitem0 3 = S$UpgradeDetailExcute[0][3]
  CHANGEITEMADDVALUE  boxitem0 4 = S$UpgradeDetailExcute[0][4]
  mov N$IsUpgrade 1
#or
  equal N$StdMode 10
  equal N$StdMode 11
  equal N$StdMode 15
  equal N$StdMode 26
  equal N$StdMode 22
  equal N$StdMode 62
  equal N$StdMode 64
  ;普通位置强化位置 0,1,2,3,4
#act  
  CHANGEITEMADDVALUE  boxitem0 0 = S$UpgradeDetailExcute[0][0]
  CHANGEITEMADDVALUE  boxitem0 1 = S$UpgradeDetailExcute[0][1]
  CHANGEITEMADDVALUE  boxitem0 2 = S$UpgradeDetailExcute[0][2]
  CHANGEITEMADDVALUE  boxitem0 3 = S$UpgradeDetailExcute[0][3]
  CHANGEITEMADDVALUE  boxitem0 4 = S$UpgradeDetailExcute[0][4]
  mov N$IsUpgrade 1
#if
  equal N$IsUpgrade 0
#act
  messagebox 强化错误,请重试!
  UpdateItem boxitem0
  ReturnBoxItem  0
  goto  @updateBoxDialog
  break
#if
  equal N$UpgradeResult 1
  equal N$UpgradeCount 5
  CheckFluteCount boxitem0 < 1
#act
  SETFLUTECOUNT boxitem0 1
  SendCenterMsg 180 251 你的装备『<$str(S$EquipName)>』已经升级到了Lv.<$str(N$UpgradeCount)>级,获得一个宝石镶嵌孔洞! 0 
  SendCenterMsg 253 251 <$USERNAME>的装备『<$str(S$EquipName)>』已经升级到了Lv.<$str(N$UpgradeCount)>级,获得一个宝石镶嵌孔洞! 1 

#if
  equal N$UpgradeResult 1
  equal N$UpgradeCount 9
  CheckFluteCount boxitem0 < 2
#act
  SETFLUTECOUNT boxitem0 2
  SendCenterMsg 180 251 你的装备『<$str(S$EquipName)>』已经升级到了Lv.<$str(N$UpgradeCount)>级,获得一个宝石镶嵌孔洞! 0 
  SendCenterMsg 253 251 <$USERNAME>的装备『<$str(S$EquipName)>』已经升级到了Lv.<$str(N$UpgradeCount)>级,获得一个宝石镶嵌孔洞! 1 
  
#if
  equal N$UpgradeResult 1
  equal N$UpgradeCount 13
  CheckFluteCount boxitem0 < 3
#act
  SETFLUTECOUNT boxitem0 3
  SendCenterMsg 180 251 你的装备『<$str(S$EquipName)>』已经升级到了Lv.<$str(N$UpgradeCount)>级,获得一个宝石镶嵌孔洞! 0 
  SendCenterMsg 253 251 <$USERNAME>的装备『<$str(S$EquipName)>』已经升级到了Lv.<$str(N$UpgradeCount)>级,获得一个宝石镶嵌孔洞! 1 
   

#if
  equal N$UpgradeResult 1
  equal N$UpgradeCount 12
#act  
  SendCenterMsg 253 251 <$USERNAME>成功的将装备『<$str(S$EquipName)>』强化到Lv.<$str(N$UpgradeCount)>,太厉害了,有谁还能超越他吗! 1 

#if
  equal N$UpgradeResult 1
  equal N$UpgradeCount 13
#act  
  SendCenterMsg 253 251 <$USERNAME>成功的将装备『<$str(S$EquipName)>』强化到Lv.<$str(N$UpgradeCount)>,太厉害了,有谁还能超越他吗! 1 

#if
  equal N$UpgradeResult 1
  equal N$UpgradeCount 14
#act  
  SendCenterMsg 253 251 <$USERNAME>成功的将装备『<$str(S$EquipName)>』强化到Lv.<$str(N$UpgradeCount)>,太厉害了,有谁还能超越他吗! 1 

#if
  equal N$UpgradeResult 1
  equal N$UpgradeCount 15
#act  
  SendCenterMsg 253 251 <$USERNAME>成功的将装备『<$str(S$EquipName)>』强化到Lv.<$str(N$UpgradeCount)>,神兵出世,天下震惊!! 1 

#if
  equal N$UpgradeResult 1
  equal N$UpgradeCount 16
#act  
  SendCenterMsg 253 251 <$USERNAME>成功的将装备『<$str(S$EquipName)>』强化到Lv.<$str(N$UpgradeCount)>,神兵出世,天下震惊!! 1 

#if
  equal N$UpgradeResult 1
  equal N$UpgradeCount 17
#act  
  SendCenterMsg 253 251 <$USERNAME>成功的将装备『<$str(S$EquipName)>』强化到Lv.<$str(N$UpgradeCount)>,神兵出世,无出其右,天下震惊!!! 1 

#if
  equal N$UpgradeResult 1
  equal N$UpgradeCount 18
#act  
  SendCenterMsg 253 251 <$USERNAME>成功的将装备『<$str(S$EquipName)>』强化到Lv.<$str(N$UpgradeCount)>,神兵出世,天下无双,大家快去围观吧!!! 1 


#if
  equal N$UpgradeResult 1  
#act  
  SendCenterMsg 180 251 强化成功!你的『<$str(S$EquipName)>』发出一阵耀眼的光芒,!属性获得了极大的提升! 0 
  
#if
#act  
  mov N$IsUpgrade 0
  ChangeItemUpgradeCount boxitem0 = <$str(N$UpgradeCount)>
  UpdateItem boxitem0
  goto  @updateBoxDialog
  
  
[@使用幸运石]
#if
  EQUAL N$IsLuck 7
#act
  mov N$IsLuck 6
#elseact  
  mov N$IsLuck 7
#if
#act
  goto @显示强化系统对话框
    
[@使用守护符]
#if
  EQUAL N$IsProtect 7
#act
  mov N$IsProtect 6
  goto @显示强化系统对话框
#elseact  
  mov N$IsProtect 7
  goto @显示强化系统对话框
}